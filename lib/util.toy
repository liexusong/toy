# $Id: util.toy,v 1.14 2011/12/09 12:54:41 mit-sato Exp $

#
# utilitie functions and methods
#

#
# apply constructor delegate chain
# Using: xx method () init {apply-constructor}
#
defun apply-constructor () {
    [[self] delegate?] each do: {| i |
	apply-constructor-sub $i;
    };
};

defun apply-constructor-sub (o) {
    set l [$o delegate?];
    if {$l null?} then: {return};

    $l each do: {| i |
	apply-constructor-sub $i;
	try {
	    [$i var? init];
	}
        catch: {};
    };
};

#
# block eval and input/output redirect from/to specified file.
#
Block method >> (file) {
    try {
        set orig_stdout $stdout;
        setvar stdout [new File];
        $stdout open $file mode: o;
	[self] eval;
    }
    catch: {| e |
        throw $e;
    }
    fin: {
        try {$stdout close;} catch: {};
        setvar stdout $orig_stdout;
    };
};

Block method << (file) {
    try {
        set orig_stdin $stdin;
        setvar stdin [new File];
        $stdin open $file mode: i;
	[self] eval;
    }
    catch: {| e |
        throw $e;
    }
    fin: {
        try {$stdin close;} catch: {};
        setvar stdin $orig_stdin;
    };
};

#
# List utility
#
List method clone () {
    set result ();
    [self] each do: {| l |
	$result + $l;
    };
    return $result;
};

List method insert! (pos obj) {
    [seek $pos] << $obj;
    return [self];
};

List method delete! (pos) {
    if [$pos >= [[len] - 1]]
    then: {->>}
    else: {[seek $pos] >>};
    return [self];
};

List method insert (pos obj) {
    set result [[self] clone];
    [$result seek $pos] << $obj;
    return $result;
};

List method delete (pos) {
    set result [[self] clone];
    if [$pos >= [[len] - 1]]
    then: {$result ->>}
    else: {[$result seek $pos] >>};
    return $result;
};

List method find (obj) {
    set i 0;
    [self] each do: {| f |
	if [$f = $obj] then: {return $i};
	$i ++;
    };
    return nil;
};

List method find* (obj) {
    set i 0;
    set result ();
    [self] each do: {| f |
	if [$f = $obj] then: {$result + $i};
	set i [$i + 1];
    };
    return $result;
};

List method reverse () {
    set result ();
    [self] each do: {| i |
	set result [($i) concat $result];
    };
    return $result;
};

#
# string method
#
Hash method string () {
    "`Hash" . [pairs];
};

Array method string () {
    "`Array" . [list];
};

#
# Hash::each
#
Hash method each (do: body) {
    if {set? body} else: {throw ErrNoBlock "No specified block."};
    [keys] each do: {| k | yield $body $k [self],$k};
};

#
# Object::_
#
Object method _ (args: a) {
    if [[$a len] = 0] then: {return [self]};

    set o [self];
    set l ();
    $a each do: {| p |
	if {["" . $p] = "_"}
	then: {
	    set o [call $o $l];
            set l ();
	}
	else: {
	    $l append! $p;
        };
    };

    call $o $l;
};

#
# File::each
#
File method each (do: body) {
    while {! [eof?]} do: {
	yield $body [gets];
    };
};

#
# Dict util methods
#
Dict method each (do: body) {
    if {set? body} else: {throw ErrNoBlock "No specified block."};
    [keys] each do: {| k | yield $body $k [self],$k};
};

Dict method string () {
    "[dict " . [pairs] "]";
};

#
# Vector util method
#
###Vector method each (do: body) {
###    if {set? body} else: {throw ErrNoBlock "No specified block."};
###    [list] each do: {| i | yield $body $i};
###};

Vector method string () {
    "[vector " . [list] "]";
};

#
# IPv4 utils
#

# Using: resolv-host host-name or ip-addr-string.
defun resolv-host (host) {
    try {
	set a [[[resolv-in-addr $host] split sep: "."] map {| i | $i int}];

	if [[$a len] != 4] then: {
	    throw ErrBadAddrSpec "Bad inet address spec.";
	};

	$a each do: {| i |
	    if [$i > 255] then: {
		throw ErrBadAddrSpec "Bad inet address spec.";
	    };
	};

	return [([$a,0 << 24] [$a,1 << 16] [$a,2 << 8] $a,3)
		inject 0 do: {| s i | $s + $i}];

    } catch: {| e |
	throw $e;
    };
};
